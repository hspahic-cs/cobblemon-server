import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id("java")
    id("net.neoforged.moddev") version "2.0.78"
    kotlin("jvm") version "2.2.20"
}

version = project.property("mod_version") as String
group = project.property("maven_group") as String

repositories {
    mavenCentral()
    maven("https://artefacts.cobblemon.com/releases")
    maven("https://thedarkcolour.github.io/KotlinForForge/")
}

neoForge {
    version = project.property("neoforge_version") as String

    runs {
        register("server") { server(); programArguments.add("--nogui") }
        register("client") { client() }
    }

    mods {
        register("cobblemon_carrots") {
            sourceSet(sourceSets.main.get())
        }
    }
}

dependencies {
    implementation("thedarkcolour:kotlinforforge-neoforge:${project.property("kotlin_for_forge_version")}")
    implementation("com.cobblemon:neoforge:${project.property("cobblemon_version")}")

    // Mixin annotation API — runtime is provided by NeoForge. SmartphoneHealMixin targets a class
    // from Cobblemon Smartphone (not on our compile classpath); with remap=false the Mixin applies
    // directly at runtime against Mojmap names, so no AP/refmap is needed. Mirrors the bridge's
    // cross-mod mixin setup (UnchainedIvNotifyThrottleMixin).
    compileOnly("org.spongepowered:mixin:0.8.7")

    testImplementation(kotlin("test"))
    testImplementation("org.junit.jupiter:junit-jupiter:5.10.2")
    testImplementation("org.slf4j:slf4j-api:2.0.9")
    testRuntimeOnly("org.slf4j:slf4j-simple:2.0.9")
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks {
    test { useJUnitPlatform() }
    processResources {
        inputs.property("version", project.version)
        filesMatching("META-INF/neoforge.mods.toml") { expand(project.properties) }
    }
    compileJava { options.release = 21 }
    compileKotlin { compilerOptions { jvmTarget.set(JvmTarget.JVM_21) } }
}
